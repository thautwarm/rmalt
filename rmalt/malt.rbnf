import std.common.[Name Space DoubleQuotedStr Comment]
pyimport rmalt.asdl.[*]
pyimport rmalt.op_priority.[*]
pyimport rmalt.std.[op_priorities]
pyimport functools.[reduce]
ignore[Space]

_keyword cast := 'let' 'include' 'not' 'cond' 'true' 'false' 'nil' 'or' 'and' 'infix'

Number := R'\d+'

Lambda ::= '(' [NameList as params] ')' '->' Expr as body
            rewrite
                ASDL.Lambda(params, body)

Numeric ::= ['-' as neg] Number as integral ['.' Number as floating]
            rewrite
                r = float(integral.value + '.' + floating.value) if floating else int(integral.value)
                ASDL.Const.num(-r if neg else r)

MStr    ::= DoubleQuotedStr+ as seq
            rewrite
                ASDL.Const.str(''.join(map(lambda _: _.value, seq)))

NamedConst ::= 'true' as true | 'false' as false | 'nil' as nil
            rewrite
                ASDL.Const.true  if true  else\
                ASDL.Const.false if false else\
                ASDL.Const.nil

InfixDef ::= 'infix' BinOp as op [Number as priority]
            rewrite
                op = op.box.name
                
                if priority:
                    priority = int(priority.value) 
                    op_priorities[op] = priority 
                else:
                    priority = op_priorities.get(op)
                
                ASDL.InfixDef(op, priority)

Sentence ::=| Let      as ret
            | Expr     as ret
            | Include  as ret
            | InfixDef as ret 
            rewrite
                ret

SentenceList ::= (Sentence ('|' Sentence)*) as seq
                 rewrite
                    tuple(seq[::2])

Include ::= 'include' DoubleQuotedStr as where_
        rewrite
            ASDL.Include(where_.value)


Cond ::= 'cond' '{' (Map ('|' Map)*) as map_seq '}'
         rewrite
            ASDL.Cond(tuple(map_seq[::2]))

Map  ::= Expr as case '=>' Sentence as do
        rewrite
            ASDL.Map(case, do)

Identifier ::= Name as name | '(' BinOp as bin_op ')'
               rewrite
                    name.value if name else\
                    bin_op.box.name

Let ::= 'let' Identifier as target '=' Expr as body
         rewrite
            ASDL.Assign(target, body)

ExprList ::= (Expr (',' Expr)*) as seq
            rewrite
                tuple(seq[::2])

Block ::= '{' SentenceList as stmts '}'
            rewrite
                ASDL.Block(stmts)

Expr ::=
        | Cond as body
        | App as body
        rewrite
            body

App ::= Binary+ as seq
        rewrite
            reduce(ASDL.Call, seq)

BinOp   ::=
        | ('or' | '-' | 'and') as elem
        | '`' Name as name '`'
        | ('*' | '^' | '%' | '&' | '@' | '$' | '+' | '/' | '>' | '<' | '=' | '.' | '?' | '!' | '::')+ as seq
        rewrite
            op = elem.value if elem else\
                 name.value if name else\
                 ''.join(map(lambda _: _.value, seq))
            Operator(ASDL.Symbol(op))

Binary ::= Factor as head (BinOp Factor)* as tail
           rewrite
            if tail:
                itor = TwoSideLink.from_iter((head, *tail))
                head = bin_expr_reduce(lambda op, a, b: ASDL.Call(ASDL.Call(op, a), b), itor)
            head

Factor ::= ['-' as neg | 'not' as _not] Atom as body
            rewrite
                ASDL.Call(ASDL.Symbol('neg'), body) if neg else\
                ASDL.Call(ASDL.Symbol('not'), body) if _not else\
                body

Atom  ::=
        | Block      as body
        | Dict       as body
        | List       as body
        | Lambda     as body
        | NamedConst as body
        | Numeric    as body
        | MStr       as body
        | Identifier as ident
        | '(' Expr   as body ')'
        | Tuple      as body
        rewrite
            if ident:
                body = ASDL.Symbol(ident)
            body


Tuple ::= '(' [ExprList as seq [',' as comma]]')'
        with
            not seq or len(seq) is not 1 or comma
        rewrite
            ASDL.Tuple(seq)

List ::= '[' [ExprList as seq]']'
        rewrite
            ASDL.List(seq)

Pair  ::= Expr as key ':' Expr as value
            rewrite
                ASDL.Pair(key, value)

Dict   ::= '{' [Pair (',' Pair)*] as seq '}'
            rewrite
                ASDL.Dict(tuple(seq[::2]))



NameList ::= (Name (',' Name)*) as seq
            rewrite
                tuple(map(lambda _: _.value, seq[::2]))

Grammar ::= SentenceList as stmts
            rewrite
                ASDL.Block(stmts)



















